#
# Complex numbers
#

la rr1 u32{
      co=010001,
      nwords=2,
      rr1=reg(25,21),
      u32=inm(63,32),
      # MAR <- PC
      # PC <- PC + 4; MBR <- Mem[MAR]
      # RR1 <- MBR; Fetch
      {
          (T2, C0),
          (M2, C2, R, TA, M1, BW=11, C1),
          (SELC=10101, LC, T1, A0=1, B=1, C=0)
      }
}

sc rr1 rr2 rr3{
      co=010010,
      nwords=1,
      rr1=reg(25,21),
      rr2=reg(20,16),
      rr3=reg(15,11),
      # MAR <- RR3; RT1 <- RR3
      # MBR <- RR1; 
      # Mem[MAR] <- MBR
      # MAR <- RT1 + 4
      # MBR <- RR2
      # Mem[MAR] <- MBR; Fetch
      {
          (SELA=01011, T9, C0, C4),      
          (SELA=10101, T9, C1),
          (BW=11, TD, TA, W),
          (MA, SELCOP=1010, MC, MB=10, T6, C0),
          (SELA=10000, T9, C1),
          (BW=11, TD, TA, W, A0=1, B=1, C=0),
      }
}

lc rr1 rr2 rr3{
      co=010011,
      nwords=1,
      rr1=reg(25,21),
      rr2=reg(20,16),
      rr3=reg(15,11),
      # MAR <- RR3; RT1 <- RR3
      # MBR <- Mem[MAR]; 
      # RR1 <- MBR
      # MAR <- RT1 + 4
      # MBR <- Mem[MAR]; 
      # RR2 <- MBR; Fetch
      {
          (SELA=01011, T9, C0, C4),      
          (R, TA, M1, BW=11, C1),
          (T1, SELC=10101, LC),
          (MA, SELCOP=01010, MC, MB=10, T6, C0),
          (R, TA, M1, BW=11, C1),
          (T1, SELC=10000, LC, A0=1, B=1, C=0),
      }
}

addc rr1 rr2 rr3 rr4{
      co=010100,
      nwords=1,
      rr1=reg(25,21),
      rr2=reg(20,16),
      rr3=reg(15,11),
      rr4=reg(10,6),
      # RR1 <- RR1 + RR3 (SR)
      # RR2 <- RR2 + RR4; Fetch
      {
          (SELCOP=01010, MC, SELA=10101, SELB=01011, T6, SELC=10101, LC, SELP=11, M7, C7),      
          (SELCOP=01010, MC, SELA=10000, SELB=00110, T6, SELC=10000, LC, SELP=00, A0=1, B=1, C=0)
      }
}

mulc rr1 rr2 rr3 rr4{
      co=010101,
      nwords=1,
      rr1=reg(25,21),
      rr2=reg(20,16),
      rr3=reg(15,11),
      rr4=reg(10,6),
      # RT1 <- RR1 * RR3
      # RT2 <- RR2 * RR4
      # RT3 <- RT1 - RT2 (SR)
      # RT1 <- RR1 * RR4
      # RT2 <- RR2 * RR3
      # RR2 <- RT1 + RT2
      # RR1 <- RT3; Fetch
      {
          (SELCOP=01100 , MC, SELA=10101, SELB=01011, T6, C4),
          (SELCOP=01100 , MC, SELA=10000, SELB=00110, T6, C5),
          (SELCOP=01011, MC, MA, MB=01, C6, SELP=11, M7, C7)
          (SELCOP=01100 , MC, SELA=10101, SELB=00110, T6, C4),
          (SELCOP=01100 , MC, SELA=10000, SELB=01011, T6, C5),
          (SELCOP=01010, MC, MA, MB=01, T6, SELC=10000, LC),
          (T7, SELC=10101, LC, A0=1, B=1, C=0)
      }
}

beqc rr1 rr2 rr3 rr4 s6{
      co=110100,
      nwords=1,
      rr1=reg(25,21),
      rr2=reg(20,16),
      rr3=reg(15,11),
      rr4=reg(10,6),
      s6=address(5,0)rel, 
      # RT1 <- RR1 - RR3
      # RT2 <- RR2 - RR4
      # MBR <- SR
      # Z <- !(RT1 OR RT2); SI Z ES 0, SALTAR A beqc_verdadero (Se actualiza SR)
      # SR <- MBR (Restaurar SR)
      # Fetch (Si da Falso)
      # beqc_verdadero: 
      # RT1 <- PC
      # RT2 <- S6
      # PC <- RT1 + RT2 
      # Fetch
      {
        (SELCOP=01011, MC, SELA=10101, SELB=01011, T6, C4),
        (SELCOP=01011, MC, SELA=10000, SELB=00110, T6, C5),
        (T8, C1)
        (SELCOP=00100, MC, MA, MB=01, SELP=11, M7, C7),
        (T1, C7)
        (C=110, B=0, A0=0, MADDR=beqc_verdadero)
        (A0=1, B=1, C=0),
    beqc_verdadero:
        (T2, C4),
        (T3, SIZE=110, C5, SE=1),
        (SELCOP=01010, MC, MA, MB=01, T6, C2),
        (A0=1, B=1, C=0)
      }
}

call u20{
      co=100001,
      nwords=1,
      u20=address(19,0)abs,
      # RA <- PC
      # PC <- U20
      {
        (SELC=1, LC, MR, T2),
        (SIZE=10011, T3, C2, A0=1, B=1, C=0)
      }

}

ret{
      co=100010,
      nwords=1,
      # RA <- PC
      {
        (SELA=1, MR, T9, C2, A0=1, B=1, C=0)
      }

}

hcf{
      co=100011,
      nwords=1,
      # PC <- 0; SR <- 0
      {
        (C2, C7, EXCODE=0, T11, A0=1, B=1, C=0)
      }

}